
// The production credentials are only used on the main branch, when the build is triggered by code push or a human
// On branches, PR, or the daily rebuild, then tests are run, no deployment are executed, and the "non production" credentials are used
def CREDENTIAL_BASE_ID = 'ci-terraform'
if (env.BRANCH_NAME == 'main' && !currentBuild.getBuildCauses('hudson.triggers.TimerTrigger$TimerTriggerCause')) {
  CREDENTIAL_BASE_ID = 'production-terraform'
}

pipeline {
  triggers {
    // Build once per day if not a Pull Request or not a tag build
    cron(env.CHANGE_ID || env.TAG_NAME ? '' : 'H 1 * * *')
  }

  agent {
    kubernetes {
      yaml """
apiVersion: v1
kind: Pod
spec:
  containers:
  - name: terraform
    image: jenkinsciinfra/terraform@sha256:8b9c95c1e8206f812d6946657ef73b0210c8e4d7143b71778afd79ef31632cee # 1.1.0 tag
    command:
    - cat
    tty: true
"""
      defaultContainer('terraform')
    }
  }

  options {
    buildDiscarder(logRotator(numToKeepStr: '5'))
    timeout(time: 2, unit: 'HOURS')
  }

  environment {
    AWS_ACCESS_KEY_ID = credentials("${CREDENTIAL_BASE_ID}-access-key")
    AWS_SECRET_ACCESS_KEY = credentials("${CREDENTIAL_BASE_ID}-secret-key")
  }

  stages {
    stage('ðŸ§¹ Prepare Terraform Environment') {
      steps {
        sh 'make prepare'
      }
    }
    stage('ðŸ”Ž Validate Terraform Project') {
      steps {
        sh 'make validate'
      }
    }
    stage('âœ… Test Terraform Project') {
      when {
        // Do not run tests with the production credentials to avoid polluting production Terraform workspaces
        expression {
          return CREDENTIAL_BASE_ID != 'production-terraform'
        }
      }
      steps {
        sh 'make tests'
      }
    }
  }
}
